#!/usr/bin/env coffee

optimist = require 'optimist'
defaults = require '../lib/defaults'
path = require 'path'

cwd = process.cwd()

optimist.usage '''
  Usage:
    haw build --config ./package.json --output ./build
    haw serve --port 1234
'''

options = optimist.options({
  config: alias: 'c', description: 'Configuration file'
  root: alias: 'r', description: 'Root from which to look for files'
  output: alias: 'o', description: 'Directory in which to output the site'
  port: alias: 'p', description: 'Port on which to run the server'
  version: alias: 'v', description: 'Print the version number'
  help: alias: 'h', description: 'Print some help'
}).argv

[command, commandArgs...] = options._

config = try require path.resolve cwd, options.config
config ?= try require path.resolve cwd, options.root || '', 'slug'

if typeof config is 'function'
  clone = require 'clone'
  targetConfig = clone defaults
  config.call targetConfig, options
  config = targetConfig

config ?= {}

config[option] = value for option, value of options when option of defaults

command = 'version' if options.version
command = 'help' if options.help

switch command
  when 'serve', 'server'
    Server = require '../lib/server'
    [port] = commandArgs
    (new Server config).serve port, options

  when 'build', 'builder'
    Builder = require '../lib/builder'
    [output] = commandArgs
    (new Builder config).build output, options

  when 'help'
    optimist.showHelp() # TODO

  when 'version'
    console.log (require path.join __dirname, '..', 'package').version

  else
    optimist.showHelp()
